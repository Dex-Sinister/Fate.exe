@create +Fate Object

@@ Set DB_SHEET to the DBref of the +Sheet Object. This example assumes that the +Sheet Object is #6.
&DB_SHEET +Fate Object=#6

@link +Fate Object = #2
@set +Fate Object = WIZARD !NO_COMMAND
&ANSI +Fate Object=ansi(yh,strfirstof(%0,<+fate>))
&CMD +Fate Object=
&CMD`FATE +Fate Object=$+fate:@pemit %#=u(ansi) [u(fun`fate`show,%#,1)]
&CMD`FATE-PLAYER +Fate Object=$+fate *:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Sets player into %q0 and sheet into %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[u(fun`fate`show,%q0,match(%#,%q0))])]
&CMD`FATE/ACCEPT +Fate Object=$+fate/accept *:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Saves player in %q0 and name in %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Do you have a compel from them?)]hasattr(%#,_compel`%q0),You don't have a compel from %qn.,You accept %qn's [setr(z,compel "[get(%#/_compel`%q0)]" and now)] have [setr(zz,setr(f,inc(get(%#/_fate))) fate point[switch(%qf,1,,s)].)][pemit(%#,u(ansi) %k accepts your %qz has %q<zz>)][oemit(%# %q0,u(ansi) %k accepts %qn's %qz has %q<zz>)][cemit(+Fate,%k accepts %qn's %qz has %q<zz>,1)][attrib_set(%#/_compel`%q0)][attrib_set(%#/_fate,%qf)])]
&CMD`FATE/AWARD +Fate Object=$+fate/award *=*:@pemit %#=u(ansi) [switch(0,[@@(Does %0 match a player? Sets player into %q0 and name into %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Is %q0 different from the enactor?)]not(match(%#,%q0)),You may not award fate points to yourself. Instead[chr(44)] use [u(ansi,+fate/gain)].,[@@(Is there any text?)]strlen(%1),You must provide justification.,You award %qn [setr(z,one fate point with justification "%1".)] [capstr(subj(%q0))] now has [setr(zz,[setr(f,inc(get(%q0/_fate)))] fate point[switch(%qf,1,,s)].)][attrib_set(%q0/_fate,%qf)][pemit(%q0,u(ansi) %k awards you %qz You now have %q<zz>)][oemit(%# %q0,u(ansi) %k awards %qn %qz [capstr(subj(%q0))] now has %q<zz>)][cemit(+Fate,%k awards %qn %qz,1)])]
&CMD`FATE/CANCEL +Fate Object=$+fate/cancel *:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Saves player in %q0 and name in %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Do they have a compel from you?)]hasattr(%q0,_compel`%#),%qn doesn't have a compel from you.,You cancel %qn's compel.[pemit(%q0,u(ansi) %k cancels your compel.)][attrib_set(%q0/_compel`%#)])]
&CMD`FATE/CHECK +Fate Object=$+fate/check:@pemit %#=u(ansi) [switch(nattr(%#/_compel`*),0,You haven't been offered any compels.,1,[moniker(last(setr(a,lattr(%#/_compel`*)),`))] offered you the compel "[get(%#/%qa)]",Your compels:%r%r[iter(sortby(#lambda/comp(name(last(%%0,`)),name(last(%%1,`))),lattr(%#/_compel`*)),moniker(last(%i0,`)) offered you the compel "[get(%#/%i0)]",,%r)])]
&CMD`FATE/CHECK-PLAYER +Fate Object=$+fate/check *:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Saves player in %q0 and name in %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Do they have a compel from you?)]hasattr(%q0,_compel`%#),%qn doesn't have a compel from you.,You offered %qn the compel "[get(%q0/_compel`%#)]")]
&CMD`FATE/EDIT +Fate Object=$+fate/edit *=*:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Saves player in %q0 and name in %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Do they have a compel from you?)]hasattr(%q0,_compel`%#),%qn doesn't have a compel from you.,[@@(Have you provided text?)]strlen(%1),You must provide text. To erase this compel[chr(44)] use [u(ansi,+fate/cancel %qn)].,You edit %qn's [setr(z,compel to "%1")][pemit(%q0,u(ansi) %k edits your %qz)][attrib_set(%q0/_compel`%#,%1)])]
&CMD`FATE/GAIN +Fate Object=$+fate/gain *:@pemit %#=u(ansi) You gain [setr(z,a fate point[chr(44)] with justification "%0")] You now have [setr(zz,[setr(f,inc(get(%#/_fate)))] fate point[switch(%qf,1,,s)].)][attrib_set(%#/_fate,%qf)];@oemit %#=u(ansi) %k gains %qz %S now has %q<zz>;@cemit/noisy +Fate=%k gains %qz
&CMD`FATE/GAIN-NOARG +Fate Object=$+fate/gain:@pemit %#=u(ansi) You must provide justification for your fate point gain.
&CMD`FATE/HERE +Fate Object=$+fate/here:@pemit %#=[iter(%# [setdiff(lcon(%l,connect),%#,%b,namei)],u(ansi) [u(fun`fate`show,%i0,match(%#,%i0))],,%r)]
&CMD`FATE/NPC/CLEAR +Fate Object=$+fate/npc/clear:@pemit %#=u(ansi) [switch(0,[@@(Do they have NPC FP?)]hasattr(%#/_fate`npc),You already have no NPC FP.,You clear your NPC FP pool.[oemit(%#,u(ansi) %k clears %p NPC FP pool.)][attrib_set(%#/_fate`npc)])]
&CMD`FATE/NPC/GAIN +Fate Object=$+fate/npc/gain *:@pemit %#=u(ansi) You gain [setr(z,an NPC FP[chr(44)] with justification "%0")] You now have [setr(zz,[setr(f,inc(get(%#/_fate`npc)))] NPC FP.)][attrib_set(%#/_fate`npc,%qf)];@oemit %#=u(ansi) %k gains %qz %S now has %q<zz>
&CMD`FATE/NPC/REFRESH +Fate Object=$+fate/npc/refresh *:@pemit %#=u(ansi) [switch(1,[@@(Is %0 an int?)]cand(isint(%0),gt(%0,0)),You refresh your [setr(z,NPC FP to %0.)][oemit(%#,u(ansi) %k refreshes %p %qz)][attrib_set(%#/_fate`npc,%0)],[@@(Is %0 of the format x#, with # being an int?)]cand(strmatch(%0,x*),isint(setr(0,delete(%0,0,1))),gt(%q0,0)),You refresh your [setr(z,NPC FP to %q0 for each other player[chr(44)] or [setr(f,mul(%q0,dec(words(lcon(%l,connect)))))].)][oemit(%#,u(ansi) %k refreshes %p %qz)][attrib_set(%#/_fate`npc,%qf)],Error: NPC FP refresh amount must be either a whole number greater than zero[chr(44)] or 'x' with a whole number greater than zero.)]
&CMD`FATE/NPC/REFRESH-NOARG +Fate Object=$+fate/npc/refresh:@pemit %#=u(ansi) You refresh your [setr(z,NPC FP to 1 for each other player[chr(44)] or [setr(f,mul(1,dec(words(lcon(%l,connect)))))].)][oemit(%#,u(ansi) %k refreshes %p %qz)][attrib_set(%#/_fate`npc,%qf)]
&CMD`FATE/NPC/SPEND +Fate Object=$+fate/npc/spend *:@pemit %#=u(ansi) [switch(0,[@@(Do you have enough NPC FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate`npc)),0),You don't have any NPC FP to spend.,You spend [setr(z,one NPC FP[chr(44)] with justification "%0")] You now have [setr(zz,[setr(f,dec(%qf))] NPC FP.)][oemit(%#,u(ansi) %k spends %qz %S now has %q<zz>)][attrib_set(%#/_fate`npc,%qf)])]
&CMD`FATE/NPC/SPEND-NOARG +Fate Object=$+fate/npc/spend:@pemit %#=u(ansi) [switch(0,[@@(Do you have enough NPC FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate`npc)),0),You don't have any NPC FP to spend.,You spend one NPC FP. You now have [setr(zz,[setr(f,dec(%qf))] NPC FP.)][oemit(%#,u(ansi) %k spends one NPC FP. %S now has %q<zz>)][attrib_set(%#/_fate`npc,%qf)])]
&CMD`FATE/NPC/SPEND-SLASH +Fate Object=$+fate/npc/spend/*:@pemit %#=u(ansi) [switch(0,[@@(Do you have enough NPC FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate`npc)),0),You don't have any NPC FP to spend.,[@@(Does <player> match an actual player? Sets player into %q0 and name into %qn.)]t(setr(0,locate(%#,first(first(%0),/),Pym)))[setq(n,moniker(%q0))],'[first(first(%0),/)]' is not a player.,[@@(Are they targeting NPCs? Sets 'is NPC' into %q<NPC>.)]or(not(strmatch(first(%0),*/*)),setr(npc,match(last(first(%0),/),NPC))),After a player name[chr(44)] [u(ansi,+fate/npc/spend)] only accepts '[u(ansi,/NPC)]'.,[@@(Are they /not/ targeting their own NPC?)]nand(%q<NPC>,match(%#,%q0)),You can't spend NPC FP into your own NPC FP pool.,You spend [setr(z,one NPC FP against %qn[if(%q<NPC>,'s NPC)][switch(setr(j,rest(%0)),[@@(null string)],[@@(full stop)].,[chr(44)] with justification "%qj")])] You now have [setr(zz,[setr(f,dec(%qf))] NPC FP and %qn has [if(%q<npc>,[setr(f2,inc(get(%q0/[setr(a,_fate`npc)])))] NPC FP,[setr(f2,inc(get(%q0/[setr(a,_fate)])))] fate point[switch(%qf,1,,s)])].)][oemit(%#,u(ansi) %k spends %qz %S now has %q<zz>)][attrib_set(%#/_fate`npc,%qf)][attrib_set(%q0/%qa,%q<f2>)])]
&CMD`FATE/OFFER +Fate Object=$+fate/offer *=*:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Saves player in %q0 and name in %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Are the two players different?)]not(match(%q0,%#)),You don't offer yourself compels. Please use [u(ansi,+fate/gain %1)],[@@(Is there text?)]strlen(%1),You must provide compel text.,You offer %qn [setr(z,the compel "%1")][pemit(%q0,u(ansi) %k offers you %qz To accept and gain a fate point[chr(44)] use '[u(ansi,+fate/accept %n)]'. To refuse and spend a fate point[chr(44)] use '[u(ansi,+fate/refuse %n)]'. Don't forget you can negotiate!)][attrib_set(%q0/_compel`%#,%1)])]
&CMD`FATE/REFRESH +Fate Object=$+fate/refresh:@pemit %#=u(ansi) [switch(0,[@@(Are their FP < their refresh value? Sets FP into %qf and refresh into %qr.)]lt(setr(f,get(%#/_fate)),setr(r,u(v(db_sheet)/fun`refresh`current,%#))),You already have %qf fate point[switch(%qf,1,,s)] and a refresh value of only %qr. To reset your fate points to your refresh value anyway[chr(44)] use [u(ansi,+fate/reset)].,You refresh to %qr fate point[switch(%qr,1,,s)].[attrib_set(%#/_fate,%qr)])]
&CMD`FATE/REFUSE +Fate Object=$+fate/refuse *:@pemit %#=u(ansi) [switch(0,[@@(Valid player? Saves player in %q0 and name in %qn.)]t(setr(0,locate(%#,%0,Pym)))[setq(n,moniker(%q0))],'%0' is not a player.,[@@(Do you have a compel from them?)]hasattr(%#,_compel`%q0),You don't have a compel from %qn.,[@@(Do you have enough FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate)),0),You don't have enough fate points to refuse a compel.,You refuse %qn's [setr(z,compel "[get(%#/_compel`%q0)]" and now)] have [setr(zz,setr(f,dec(%qf)) fate point[switch(%qf,1,,s)].)][pemit(%#,u(ansi) %k refuses your %qz has %q<zz>)][oemit(%# %q0,u(ansi) %k refuses %qn's %qz has %q<zz>)][cemit(+Fate,%k refuses %qn's %qz has %q<zz>,1)][attrib_set(%#/_compel`%q0)][attrib_set(%#/_fate,%qf)])]
&CMD`FATE/RESET +Fate Object=$+fate/refresh:@pemit %#=u(ansi) You reset to [setr(r,u(v(db_sheet)/fun`refresh`current,%#))] fate point[switch(%qr,1,,s)].[attrib_set(%#/_fate,%qr)]
&CMD`FATE/SPEND +Fate Object=$+fate/spend *:@pemit %#=u(ansi) [switch(0,[@@(Do they have enough FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate)),0),You don't have any FP to spend.,You spend [setr(z,one fate point[chr(44)] with justification "%0".)] You now have [setr(zz,[setr(f,dec(%qf))] fate point[switch(%qf,1,,s)].)][oemit(%#,u(ansi) %k spends %qz %S now has %q<zz>)][cemit(+Fate,%k spends a fate point[chr(44)] with justification "%0",1)][attrib_set(%#/_fate,%qf)])]
&CMD`FATE/SPEND-NOARG +Fate Object=$+fate/spend:@pemit %#=u(ansi) [switch(0,[@@(Do they have enough FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate)),0),You don't have any FP to spend.,You spend one fate point. You now have [setr(zz,[setr(f,dec(%qf))] fate point[switch(%qf,1,,s)].)][oemit(%#,u(ansi) %k spends one fate point. %S now has %q<zz>)][attrib_set(%#/_fate,%qf)])]
&CMD`FATE/SPEND-SLASH +Fate Object=$+fate/spend/*:@pemit %#=u(ansi) [switch(0,[@@(Do they have enough FP? Sets FP into %qf.)]gt(setr(f,get(%#/_fate)),0),You don't have any FP to spend.,[@@(Does <player> match an actual player? Sets player into %q0 and name into %qn.)]t(setr(0,locate(%#,first(first(%0),/),Pym)))[setq(n,moniker(%q0))].,'[first(first(%0),/)]' is not a player.,[@@(Are they targetting NPCs? Sets 'is NPC' into %q<NPC>.)]or(not(strmatch(%0,*/*)),setr(npc,match(last(first(%0),/),NPC))),After a player name[chr(44)] [u(ansi,+fate/spend)] only accepts '[u(ansi,/NPC)]'.,[@@(Are they targeting an NPC, or someone else?)]cor(%q<npc>,not(match(%#,%q0))),You can't spend fate points into your own pool.,You spend [setr(z,one fate point against %qn[if(%q<NPC>,'s NPC)][switch(setr(j,rest(%0)),[@@(Null string)],[@@(Do nothing)],[chr(44)] with justification "%qj")].)] You now have [setr(zz,[setr(f,dec(%qf))] fate point[switch(%qf,1,,s)] and %qn has [if(%q<npc>,[setr(f2,inc(get(%q0/_fate`npc)))] NPC FP,[setr(f2,inc(get(%q0/_fate)))])].)][oemit(%#,u(ansi) %k spends %qz %S now has %q<zz>)][if(%qj,cemit(+Fate,%k spends a fate point[chr(44)] with justification "%qj",1))][attrib_set(%#/_fate,%qf)][attrib_set(%q0/[if(%q<NPC>,_fate`npc,_fate)],%q<f2>)])]
&FUN +Fate Object=
&FUN`FATE +Fate Object=
&FUN`FATE`SHOW +Fate Object=[@@(Takes player %0 and 'is viewer' %1. Displays that player's refresh, FP, and NPC FP.)][if(%1,You have,moniker(%0) has)] [u(v(db_sheet)/fun`refresh`current,%0)]/[get(%0/_refresh)] refresh and [setr(f,default(%0/_fate,0))] fate point[switch(%qf,1,,s)].[if(hasattr(%0,_fate`npc),%b[if(%1,You have,[capstr(subj(%0))] has)] [get(%0/_fate`npc)] NPC FP.)]

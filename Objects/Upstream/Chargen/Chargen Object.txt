@create Chargen Object

@@ Set DB_SHEET to the DBref of the +Sheet Object. This example assumes that the +Sheet Object is #6.
&DB_SHEET Chargen Object=#6

@link Chargen Object = #0
@set Chargen Object = WIZARD !NO_COMMAND
&ANSI Chargen Object=ansi(n,strfirstof(%0,<CG>))
&CMD Chargen Object=
&CMD`ASPECTS Chargen Object=$+aspects:@pemit %#=u(v(db_sheet)/fun`sheet`aspects,%#)
&CMD`ASPECTS`NAME Chargen Object=$+aspects/name *:@break strmatch(%0,*=*);@pemit %#=u(ansi) [switch(0,[@@(Is it an integer greater than zero?)]cand(isint(%0),gt(%0,0)),Aspect number must be a whole number greater than zero.,[@@(Is it within their aspect limits? Sets max aspects into %qm.)]lte(%0,setr(m,get(%#/_sheet`aspects`data`max))),You can only have %qm aspect[switch(%qm,1,,s)].,[@@(Do they have it set? Sets full aspect list into %q<aspects> and target aspect into %q0.)]strlen(first(setr(0,elements(setr(aspects,get(%#/_sheet`aspects)),%0,|)),`)),There is no aspect to erase.,You erase your aspect %0.[setq(y,1)])];@assert %qy;think [attrib_set(%#/_sheet`aspects,[replace(%q<aspects>,%0,`,|)])]
&CMD`ASPECTS`NAME_ARG Chargen Object=$+aspects/name *=*:@pemit %#=u(ansi) [switch(0,[@@(Is it an integer greater than zero?)]cand(isint(%0),gt(%0,0)),Aspect number must be a whole number greater than zero.,[@@(Is it within their aspect limits? Sets max aspects into %qm.)]lte(%0,setr(m,get(%#/_sheet`aspects`data`max))),You can only have %qm aspect[switch(%qm,1,,s)].,[@@(Have they provided a name, or do they have the aspect set? Sets full aspect list into %q<aspects> and target aspect into %q0.)]or(strlen(%1),strlen(first(setr(0,elements(setr(aspects,get(%#/_sheet`aspects)),%0,|)),`))),There is no aspect to erase.,[@@(Have they provided a name?)]strlen(%1),You erase your aspect %0.[setq(y,1)],[@@(Do they avoid illegal ` and | characters?)]not(lmath(or,iter(` |,strmatch(%1,*%i0*)))),You may not use backquote (`) or pipe (|) characters in aspect names.,You name your aspect %0 '%1'[setq(y,1)])];@assert %qy;think [attrib_set(%#/_sheet`aspects,[replace(%q<aspects>[@@(Use repeat() in case the aspect list is not actually full.)][repeat(|`,sub(%qm,words(%q<aspects>,|)))],%0,%1`,|)])]
&CMD`ASPECTS`NOTE Chargen Object=$+aspects/note *:@break strmatch(%0,*=*);@pemit %#=u(ansi) [switch(0,[@@(Is it an integer greater than zero?)]cand(isint(%0),gt(%0,0)),Aspect number must be a whole number greater than zero.,[@@(Is it within their aspect limits? Sets max aspects into %qm.)]lte(%0,setr(m,get(%#/_sheet`aspects`data`max))),You can only have %qm aspect[switch(%qm,1,,s)].,[@@(Do they have it set? Sets full aspect list into %q<aspects> and target aspect into %q0.)]strlen(last(setr(0,elements(setr(aspects,get(%#/_sheet`aspects)),%0,|)),`)),There is no note to erase.,You erase the note for your aspect %0.[setq(y,1)])];@assert %qy;think [attrib_set(%#/_sheet`aspects,[replace(%q<aspects>,%0,[first(%q0,`)]`,|)])]
&CMD`ASPECTS`NOTE_ARG Chargen Object=$+aspects/note *=*:@pemit %#=u(ansi) [switch(0,[@@(Is it an integer greater than zero?)]cand(isint(%0),gt(%0,0)),Aspect number must be a whole number greater than zero.,[@@(Is it within their aspect limits? Sets max aspects into %qm.)]lte(%0,setr(m,get(%#/_sheet`aspects`data`max))),You can only have %qm aspect[switch(%qm,1,,s)].,[@@(Have they provided a note, or do they have the aspect note set? Sets full aspect list into %q<aspects> and target aspect into %q0.)]or(strlen(%1),strlen(last(setr(0,elements(setr(aspects,get(%#/_sheet`aspects)),%0,|)),`))),There is no note to erase.,[@@(Have they provided a name?)]strlen(%1),You erase your aspect %0's note.[setq(y,1)][@@(Do they avoid illegal ` and | characters?)]not(lmath(or,iter(` |,strmatch(%1,*%i0*)))),You may not use backquote (`) or pipe (|) characters in aspect notes.,,You write your aspect %0's note:%r%r%1[setq(y,1)])];@assert %qy;think [attrib_set(%#/_sheet`aspects,[replace(%q<aspects>[@@(Use repeat() in case the aspect list is not actually full.)][repeat(|`,sub(%qm,words(%q<aspects>,|)))],%0,[replace(%q0,2,%1,`)],|)])]
&CMD`ASPECTS`WIPE Chargen Object=$+aspects/wipe:@pemit %#=u(ansi) You wipe all your aspects.[attrib_set(%#/_sheet`aspects)]
&CMD`STRESS Chargen Object=
&CMD`STRESS`UPDATE_OTHER Chargen Object=$+stress/update *:@switch 0=t(setr(0,switch(%0,me,%#,pmatch(%0)))),@pemit %#=u(ansi) '%0' is not a player.,{@pemit %#=u(ansi) You update [switch(moniker(%q0),*s,%$0',%$0's)] stress tracks.;@trigger %!/do`stress`update=%#,%q0}
&CMD`STRESS`UPDATE_OWN Chargen Object=$+stress/update:@pemit %#=u(ansi) You update your stress tracks.;@trigger %!/do`stress`update=%#,%#
&CMD`STUNTS Chargen Object=$+stunts:@pemit %#=u(v(db_sheet)/fun`sheet`stunts,%#)
&CMD`STUNTS`BUY Chargen Object=$+stunts/buy *=*:@break strmatch(%1,*=*);@pemit %#=u(ansi) [switch(0,[@@(Does %0 match a stunt list? Sets it into %q0 and the spaces-stripped one to %q1.)]complete(iter(lattr(%!/data`stunts`lists`*),iter(last(%i0,`),capstr(lcstr(%i0)),_),,|),%0,stunt list,0,|)[setq(1,edit(%q0,%b,_))],%qe,[@@(Is %1 an integer >0?)]cand(isint(%1),gt(%1,0)),Stunt number must be a whole number greater than zero.,[@@(Is %1 within the list? Sets list into %q<all>.)]lte(%1,words(setr(all,v(data`stunts`lists`%q1)),|)),The %q0 list only has [words(%q<all>,|)] stunt[switch(words(%q<all>,|),1,,s)].,[@@(Can they afford it? Sets cost result into %q<calc>, stunt into %q<stunt>, and stunt slot cost into %q<cost>.)]gt(add(get(%#/_sheet`refresh),first(setr(calc,u(v(db_sheet)/fun`stunts`calc,%#,add(u(v(db_sheet)/fun`stunts`total,%#),setr(cost,elements(setr(stunt,elements(%q<all>,%1,|)),3,~))))))),0),You don't have enough refresh left to buy %q<cost> stunt[switch(%q<cost>,1,,s)].,Attempting to buy %q0 stunt %1[chr(44)] [elements(%q<stunt>,2,~)].[setq(y,1)])];@assert %qy;think attrib_set(%!/temp_%#,[v(fun`stunts`test1)][switch(elements(%q<stunt>,5,~),:*,v(data`stunts`[delete(%$0,0,1)]),%$0)][v(fun`stunts`test2)])[setq(y,0)];@pemit %#=[u(temp_%#,%2)]
&CMD`STUNTS`BUY_ARG Chargen Object=$+stunts/buy *=*=*:@pemit %#=u(ansi) [switch(0,[@@(Does %0 match a stunt list? Sets it into %q0 and the spaces-stripped one to %q1.)]complete(iter(lattr(%!/data`stunts`lists`*),iter(last(%i0,`),capstr(lcstr(%i0)),_),,|),%0,stunt list,0,|)[setq(1,edit(%q0,%b,_))],%qe,[@@(Is %1 an integer >0?)]cand(isint(%1),gt(%1,0)),Stunt number must be a whole number greater than zero.,[@@(Is %1 within the list? Sets list into %q<all>.)]lte(%1,words(setr(all,v(data`stunts`lists`%q1)),|)),The %q0 list only has [words(%q<all>,|)] stunt[switch(words(%q<all>,|),1,,s)].,[@@(Can they afford it? Sets cost result into %q<calc>, stunt into %q<stunt>, and stunt slot cost into %q<cost>.)]gt(add(get(%#/_sheet`refresh),first(setr(calc,u(v(db_sheet)/fun`stunts`calc,%#,add(u(v(db_sheet)/fun`stunts`total,%#),setr(cost,elements(setr(stunt,elements(%q<all>,%1,|)),3,~))))))),0),You don't have enough refresh left to buy %q<cost> stunt[switch(%q<cost>,1,,s)].,Attempting to buy %q0 stunt %1[chr(44)] [elements(%q<stunt>,2,~)].[setq(y,1)])];@assert %qy;think attrib_set(%!/temp_%#,[v(fun`stunts`test1)][switch(elements(%q<stunt>,5,~),:*,v(data`stunts`[delete(%$0,0,1)]),%$0)][v(fun`stunts`test2)])[setq(y,0)];@pemit %#=[u(temp_%#,%2)]
&CMD`STUNTS`CREATE Chargen Object=$+stunts/create *=*:@pemit %#=u(ansi) [switch(0,[@@(Have they provided a name?)]strlen(%0),You must provide a stunt name.,[@@(Does the name withhold illegal characters?)]not(lmath(or,iter(~ |,strmatch(%0,*%i0*)))),You may not have tildes (~) or pipes (|) in stunt names.,[@@(Is %1 an integer?)]isint(%1),Stunt cost must be an integer.,[@@(Can they afford it? Sets cost result into %q<cost>.)]gt(add(get(%#/_sheet`refresh),first(setr(cost,u(v(db_sheet)/fun`stunts`calc,%#,add(u(v(db_sheet)/fun`stunts`total,%#),%1))))),0),You don't have enough refresh left to buy %1 stunt[switch(%1,1,,s)].,[attrib_set(%#/_sheet`stunts`stunts,[insert(get(%#/_sheet`stunts`stunts),-1,[@@(no codename)]~[@@(showname)]%0~[@@(cost)]%1~[@@(no note)]~[@@(no delcommands)],|)])]You create the stunt "%0" [switch(%1,<0,and allocate a cost of [switch(abs(%1),1,%$0 stunt,%$0 stunts)],with a refund of %1 stunt[switch(%1,1,,s)])]. You now have [add(get(%#/_sheet`refresh),first(%q<cost>))] refresh and [switch(last(%q<cost>),0,no free stunts,1,1 free stunt,%$0 free stunts)] remaining.)]
&CMD`STUNTS`DEL Chargen Object=$+stunts/del *:@pemit %#=u(ansi) [switch(0,[@@(Is %0 an integer > 0?)]cand(isint(%0),gt(%0,0)),Stunt number must be a whole number greater than zero.,[@@(Do they have that many stunts? Sets full stunt list into %q<all>, and number of stunts into %qw.)]lte(%0,setr(w,words(setr(all,get(%#/_sheet`stunts`stunts)),|))),You only have %qw stunt[switch(%qw,1,,s)].,[@@(Can they afford that? Sets calc result into %q<calc>, that stunt into %q<stunt>, and that stunt's cost into %q<cost>.)]gt(add(get(%#/_sheet`refresh),first(setr(calc,u(v(db_sheet)/fun`stunts`calc,%#,sub(u(v(db_sheet)/fun`stunts`total,%#),setr(cost,elements(setr(stunt,elements(%q<all>,%0,|)),3,~))))))),0),You can't afford to delete "[elements(%q<stunt>,2,~)]" and lose its refund.,You delete your stunt %0[chr(44)] "[elements(%q<stunt>,2,~)]"[chr(44)] for a [switch(%q<cost>,<0,refund,cost)] of [abs(%q<cost>)] stunt slot[switch(abs(%q<cost>),1,,s)]. You now have [add(get(%#/_sheet`refresh),first(%q<calc>))] refresh and [switch(last(%q<calc>),0,no free stunts,1,1 free stunt,%$0 free stunts)] remaining.[@@(If there is on-del code, run it.)][switch(elements(%q<stunt>,5,~),[@@(null string)],[@@(do nothing)],[attrib_set(%!/temp_%#,[switch(%$0,:*,get(%#/_sheet`stunts`del`[delete(%$0,0,1)]),%$0)])]%r[u(ansi)] [u(temp_%#)][attrib_set(%!/temp_%#)][attrib_set(%#/_sheet`stunts`del`[delete(%$0,0,1)])])][@@(Actually change the stunt list.)][attrib_set(%#/_sheet`stunts`stunts,[ldelete(%q<all>,%0,|)])])]
&CMD`STUNTS`NOTE Chargen Object=$+stunts/note *:@pemit %#=u(ansi) [switch(0,[@@(Is the stunt number valid? Sets stunt number into %q0.)]cand(isint(setr(0,first(%0,=))),gt(%q0,0)),Stunt number must be an integer greater than zero.,[@@(Is the stunt within their list? Sets their full stunt list into %qa and number of stunts to %qw.)]lte(%q0,setr(w,words(setr(a,get(%#/_sheet`stunts`stunts)),|))),You only have %qw stunt[switch(%qw,1,,s)].,[@@(Do they provide a comment?)]strmatch(%0,*=*),You clear the note from your stunt %q0.[attrib_set(%#/_sheet`stunts`stunts,[replace(%qa,%0,replace(elements(%qa,%0,|),4,,~),|)])],[@@(Does the note withhold illegal characters?)]not(lmath(or,iter(~ |,strmatch(rest(%0,=),*%i0*)))),You may note have tildes (~) or pipes (|) in stunt notes.,You add a note to your stunt %q0[chr(44)] reading "[setr(1,rest(%0,=))]"[attrib_set(%#/_sheet`stunts`stunts,[replace(%qa,%0,replace(elements(%qa,%0,|),4,%q1,~),|)])])]
&CMD`STUNTS/COST Chargen Object=$+stunts/cost *=*:@pemit %#=u(ansi) [switch(0,[@@(Is %0 an integer > 0?)]cand(isint(%0),gt(%0,0)),Stunt number must be a whole number greater than zero.,[@@(Do they have that many stunts? Sets full stunt list into %q<all>, and number of stunts into %qw.)]lte(%0,setr(w,words(setr(all,get(%#/_sheet`stunts`stunts)),|))),You only have %qw stunt[switch(%qw,1,,s)].,[@@(Is cost an integer?)]isint(%1),Stunt cost must be a whole number.,[@@(Can they afford it? Sets original stunt into %q<stunt> and original cost into %q<cost>.)]gt(add(get(%#/_sheet`refresh),first(setr(calc,u(v(db_sheet)/fun`stunts`calc,%#,add(sub(u(v(db_sheet)/fun`stunts`total,%#),setr(cost,elements(setr(stunt,elements(%q<all>,%0,|)),3,~))),%1))))),0),You can't afford that many stunt slots.,You change stunt %0[chr(44)] "[elements(%q<stunt>,2,~)]"[chr(44)] to a [switch(%1,<0,cost,refund)] of [abs(%1)] stunt slot[switch(abs(%1),1,,s)]. You now have [add(get(%#/_sheet`refresh),first(%q<calc>))] refresh and [switch(last(%q<calc>),0,no free stunts,1,1 free stunt,%$0 free stunts)] left.[attrib_set(%#/_sheet`stunts`stunts,[replace(%q<all>,%0,replace(%q<stunt>,3,%1,~),|)])])]
&CMD`STUNTS/LIST Chargen Object=$+stunts/list:@pemit %#=u(ansi) Stunt categories:%r%r[iter(lattr(%!/data`stunts`lists`*),iter(last(%i0,`),capstr(lcstr(%i0)),_),,%r)]
&CMD`STUNTS/LIST_ARG Chargen Object=$+stunts/list *:@pemit %#=u(ansi) [switch(0,[@@(Does %0 match a stunt list? Sets it into %q0 and the spaces-stripped one to %q1.)]complete(iter(lattr(%!/data`stunts`lists`*),iter(last(%i0,`),capstr(lcstr(%i0)),_),,|),%0,stunt list,0,|)[setq(1,edit(%q0,%b,_))],%qe,Stunts in list %q0:%r%r[iter(v(data`stunts`lists`%q1),inum(0). [elements(%i0,2,~)] ([elements(%i0,3,~)])[switch(elements(%i0,4,~),[@@(null string)],[@@(Do nothing)],%r%t%$0)],|,%r)]
&CMD`STUNTS/RENAME Chargen Object=$+stunts/rename *=*:@pemit %#=u(ansi) [switch(0,[@@(Is %0 an integer > 0?)]cand(isint(%0),gt(%0,0)),Stunt number must be a whole number greater than zero.,[@@(Do they have that many stunts? Sets full stunt list into %q<all>, and number of stunts into %qw.)]lte(%0,setr(w,words(setr(all,get(%#/_sheet`stunts`stunts)),|))),You only have %qw stunt[switch(%qw,1,,s)].,[@@(Have they provided a name?)]strlen(%1),You must provide a name.,[@@(Does the name withhold illegal characters?)]not(lmath(or,iter(~ |,strmatch(%1,*%i0*)))),You may not have tildes (~) or pipes (|) in stunt names.,You rename stunt %0 to "%1".[attrib_set(%#/_sheet`stunts`stunts,[replace(%q<all>,%0,replace(elements(%q<all>,%0,|),2,%1,~),|)])])]
&CMD`STUNTS_ARG Chargen Object=$+stunts *:@pemit %#=u(ansi) [switch(0,[@@(Does %0 match a stunt list? Sets it into %q0 and the spaces-stripped one to %q1.)]complete(iter(lattr(%!/data`stunts`lists`*),iter(last(%i0,`),capstr(lcstr(%i0)),_),,|),%0,stunt list,0,|)[setq(1,edit(%q0,%b,_))],%qe,Stunts in list %q0:%r%r[iter(v(data`stunts`lists`%q1),inum(0). [elements(%i0,2,~)] ([elements(%i0,3,~)])[switch(elements(%i0,4,~),[@@(null string)],[@@(Do nothing)],%r%t%$0)],|,%r)]
&CMD_SKILLS Chargen Object=$+skills:@pemit %#=u(v(db_sheet)/fun`sheet`skills,%#)
&CMD_SKILLS/SET_COLUMN Chargen Object=$+skills/set *=*:@assert get(%#/_sheet`skills`data`columns);@pemit %#=switch(0,[@@(Does %0 match a skill? Sets grabbed skill into %q0.)]complete(get(%#/_sheet`skills`data`list),%0,skill,0,|),%qe,[@@(Is %1 an integer >= 0?)]cand(isint(%1),gte(%1,0)),Skills cannot be rated below [ladder(0)].,[@@(Is %1 within the cap? Sets skill cap into %q<cap>.)]cor([@@(Is the character cap-free?)]lte(setr(cap,get(%#/_sheet`skills`data`cap)),0),[@@(Is %1 <= %q<cap>?)]lte(%1,%q<cap>)),You can't have skills rated above [ladder(%q<cap>)].,[@@(Is %1 different from the current rating for %q0? Sets current rating into %qa.)]neq(%1,setr(a,u(v(db_sheet)/fun`skill`rating,%#,%q0))),Your %q0 is already [ladder(%1)].,[@@(Do they have an empty slot at that rating? i.e. is the number of skills at that rating less than the number of slots?)]or(lt(%1,1),lt([@@(Number of skills.)]max(0,words(graball(setr(skills,get(%#/_sheet`skills`skills)),*`%1,|),|)),[@@(Number of slots.)]last(grab(get(%#/_sheet`skills`slots),%1`*,|),`))),You don't have an empty slot at [ladder(%1)].,[@@(Is %q0 already on the sheet?)]neq(0,%qa),You add %q0 to your sheet[chr(44)] at [ladder(%1)].[attrib_set(%#/_sheet`skills`skills,[switch(%q<skills>,,[@@(It's an empty string. This skill is all that goes in there.)]%q0`%1,[@@(It's not empty. Insert and sort as normal.)]sort(insert(%$0,-1,%q0`%1,|),a,|))])],[@@(Is %1 greater than zero?)]gt(%1,0),You remove %q0 from your sheet[chr(44)] setting it to [ladder(0)].[switch(words(setr(all,%q<skills>),|),1,[@@(Only one skill to begin with. Wipe the attribute.)]attrib_set(%#/_sheet`skills`skills),[@@(There are multiple. Remove it.)]attrib_set(%#/_sheet`skills`skills,[remove(%q<all>,%q0`%qa,|)]))],You set %q0 to [ladder(%1)].[attrib_set(%#/_sheet`skills`skills,[replace(setr(all,%q<skills>),match(%q<skills>,%q0`%qa,|),%q0`%1,|)])])
&CMD_SKILLS/SET_NOCOLUMN Chargen Object=$+skills/set *=*:@break get(%#/_sheet`skills`data`columns)=@@ The player uses columns and so does not use this version of the command.;@pemit %#=switch(0,[@@(Does %0 match a skill? Sets grabbed skill into %q0.)]complete(get(%#/_sheet`skills`data`list),%0,skill,0,|),%qe,[@@(Is %1 an integer >= 0?)]cand(isint(%1),gte(%1,0)),Skills cannot be rated below [ladder(0)].,[@@(Is %1 within the cap? Sets skill cap into %q<cap>.)]cor([@@(Is the character cap-free?)]lte(setr(cap,get(%#/_sheet`skills`data`cap)),0),[@@(Is %1 <= %q<cap>?)]lte(%1,%q<cap>)),You can't have skills rated above [ladder(%q<cap>)].,[@@(Is %1 different from the current rating for %q0? Sets current rating into %qa.)]neq(%1,setr(a,u(v(db_sheet)/fun`skill`rating,%#,%q0))),Your %q0 is already [ladder(%1)].,[@@(Do they have enough skill points? Sets skill points into %q<points>.)]gte(sub([@@(Total skill points, including refund.)]add(setr(points,u(v(db_sheet)/fun`skill`points`nocolumn,%#)),%q1),[@@(Subtract the cost of the new rating.)]%1),0),You only have %q<points> skill point[switch(%q<points>,1,,s)].,[@@(Is %q0 already on the sheet?)]neq(0,%qa),You add %q0 to your sheet[chr(44)] at [ladder(%1)].[attrib_set(%#/_sheet`skills`skills,[switch(get(%#/_sheet`skills`skills),,[@@(It's an empty string. This skill is all that goes in there.)]%q0`%1,[@@(It's not empty. Insert and sort as normal.)]sort(insert(%$0,-1,%q0`%1,|),a,|))])],[@@(Is %1 greater than zero?)]gt(%1,0),You remove %q0 from your sheet[chr(44)] setting it to [ladder(0)].[switch(words(setr(all,get(%#/_sheet`skills`skills)),|),1,[@@(Only one skill to begin with. Wipe the attribute.)]attrib_set(%#/_sheet`skills`skills),[@@(There are multiple. Remove it.)]attrib_set(%#/_sheet`skills`skills,[remove(%q<all>,%q0`%qa,|)]))],You set %q0 to [ladder(%1)].[attrib_set(%#/_sheet`skills`skills,[replace(setr(all,get(%#/_sheet`skills`skills)),match(%q<skills>,%q0`%qa,|),%q0`%1,|)])])
&CMD_SKILLS/SHIFT Chargen Object=$+skills/shift *=*:@assert get(%#/_sheet`skills`data`columns)=@pemit %#=You don't use columns, so you don't have to use +skills/shift.;@pemit %#=switch(0,[@@(Does %0 match a skill? Sets grabbed skill into %q0.)]complete(get(%#/_sheet`skills`data`list),%0,skill,0,|),%qe,[@@(Is %1 an integer?)]isint(%1),Skill rating must be a whole number.,[@@(Is %1 >= 0?)]gte(%1,0),You can't set skills below [ladder(0)].,[@@(Is %1 within the cap? Sets cap into %q<cap>.)]cor([@@(Capless?)]lte(setr(cap,get(%#/_sheet`skills`data`cap)),0),[@@(Within cap?)]lte(%1,%q<cap>)),You cannot have skills above [ladder(%q<cap>)].,[@@(Is %1 different from the current rating? Sets skills into %q<skills>, and current rating into %qa.)]neq(%1,setr(a,max(0,last(grab(setr(skills,get(%#/_sheet`skills`skills)),%q0`*,|),`)))),%q0 is already rated [ladder(%qa)].,[@@(Does the player have enough skill points?)]gte(u(v(db_sheet)/fun`skill`points`column,%#),sub(%1,%qa)),You don't have enough skill points to move %q0 from [ladder(%qa)] to [ladder(%1)].,[@@(Test if the columns are stable after removing a slot from %qa. Sets slots into %q<slots>. Sets current number of %qa slots into %q<before>.)]or(eq(%qa,0),gte([@@(Slots at %qa, -1)]dec(setr(before,last(grab(setr(slots,get(%#/_sheet`skills`slots)),%qa`*,|),`))),[@@(Slots at %qa+1.)]last(grab(%q<slots>,[inc(%qa)]`*,|),`))),You have too many slots at [ladder(inc(%qa))] to shift a skill away from [ladder(%qa)].,[@@(Is the incremented number of slots at %1 <= the number of slots below %1? Or, is %1 <= 1? Sets current number of %1 slots into %q<after>.)]or(lte(%1,1),lte(inc(setr(after,last(grab(%q<slots>,%1`*,|),`))),last(grab(%q<slots>,[dec(%1)]`*,|),`))),You don't have enough slots at [ladder(dec(%1))] to support a new skill at [ladder(%1)].,[@@(If %q0 is only being upgraded by 1, is the incremented number of slots at %1 still <= the decremented number of slots at %1-1? Or, is %1 <= 1?)]cor(lte(%1,1),neq(%1,inc(%qa)),lte(inc(last(grab(%q<slots>,%1`*,|),`)),dec(last(grab(%q<slots>,[dec(%1)]`*,|),`)))),You don't have enough slots at [ladder(dec(%1))] to support a new skill at [ladder(%1)].,[@@(Setting skills.)][attrib_set(%#/_sheet`skills`skills,[setdiff(setr(skills,switch(%q<skills>,,[@@(Null string.)]%q0`%1,sort(insert(%q<skills>,-1,%q0`%1,|),i,|))),%q0`%qa|[graball(%q<skills>,*`0,|)],|,i)])][@@(Setting slots.)][attrib_set(%#/_sheet`skills`slots,[setdiff(setr(slots,switch(%q<slots>,,[@@(Null string.)]%1`1,insert(%q<slots>,-1,%qa`[dec(%q<before>)]|%1`[inc(%q<after>)],|))),%qa`%q<before>|%1`%q<after>|[graball(%q<slots>,0`*,|)]|[graball(%q<slots>,*`0,|)],|,i)])]You shift %q0 from [ladder(%qa)] to [ladder(%1)] for a [switch(%qa,>%1,refund,cost)] of [setr(2,abs(sub(%qa,%1)))] skill point[switch(%q2,1,,s)]. You have [setr(p,u(v(db_sheet)/fun`skill`points`column,%#))] skill point[switch(%qp,1,,s)] left.)
&CMD_SKILLS/SLOTS Chargen Object=$+skills/slots *=*:@assert get(%#/_sheet`skills`data`columns)=@pemit %#=You don't use columns, so you don't have to use +skills/slots.;@pemit %#=switch(0,[@@(Is %0 a whole number?)]isint(%0),You must specify the skill rating by its number.,[@@(Is %0 > 0?)]gt(%0,0),You don't need to buy skill slots below [ladder(1)].,[@@(Is %0 within the skill cap? Sets cap into %q<cap>.)]cor(lte(setr(cap,get(%#/_sheet`skills`data`cap)),0),lte(%0,%q<cap>)),You can't currently have skills rated above [ladder(%q<cap>)].,[@@(Is %1 an integer >= 0?)]cand(isint(%1),gte(%1,0)),You must specify the number of skill slots as a whole number[chr(44)] at least zero.,[@@(Do they have enough skill points? Sets their current slots list into %q<slots> and grabbed slot into %qa.)]gte(sub([@@(Current skill points, including refund for current number of slots at that rating.)]add(u(v(db_sheet)/fun`skill`points`column,%#),mul(first(setr(a,grab(setr(slots,get(%#/_sheet`skills`slots)),%0`*,|)),`),last(%qa,`))),[@@(Cost of those slots.)]mul(%0,%1)),0),You don't have enough skill points for that purchase.,[@@(Does the player have few enough skills at %0 to go down to %1?)]lte(words(graball(get(%#/_sheet`skills`skills),*`%0,|),|),%1),You have too many skills at [ladder(%0)] to go down to %1 slot[switch(%1,1,,s)].,[@@(Does the player have few enough slots at %0+1 to go down to %1?)]lte(last(grab(%q<slots>,[inc(%0)]`*,|),`),%1),You have too many slots at [ladder(inc(%0))] to go down to %1 slot[switch(%1,1,,s)].,[@@(Is %0 1, or does the player have enough slots at %0-1 to support %1?)]cor(eq(1,%0),lte(%1,last(grab(%q<slots>,[dec(%0)]`*,|),`))),You don't have enough slots at [ladder(dec(%0))] to support %1 slot[switch(%1,1,,s)] at [ladder(%0)].,[@@(Is %1 > 0?)]gt(%1,0),[@@(Remove.)][attrib_set(%#/_sheet`skills`slots,[ldelete(%q<slots>,match(%q<slots>,%0`*,|),|)])]You now have no slots at [ladder(%0)]. You have [setr(p,u(v(db_sheet)/fun`skill`points`column,%#))] skill point[switch(%qp,1,,s)].,[@@(Do they have any slots at that level yet?)]match(%q<slots>,%0`*,|),[@@(Insert.)][attrib_set(%#/_sheet`skills`slots,[switch(%q<slots>,,[@@(Null string. This is all that needs to be set.)]%0`%1,[@@(Needs to be inserted.)]insert(%q<slots>,-1,%0`%1,|))])]You now have %1 slot[switch(%1,1,,s)] at [ladder(%0)]. You have [setr(p,u(v(db_sheet)/fun`skill`points`column,%#))] skill point[switch(%qp,1,,s)].,[@@(Replace.)][attrib_set(%#/_sheet`skills`slots,[replace(%q<slots>,match(%q<slots>,%0`*,|),%0`%1,|)])]You now have %1 slot[switch(%1,1,,s)] at [ladder(%0)]. You have [setr(p,u(v(db_sheet)/fun`skill`points`column,%#))] skill point[switch(%qp,1,,s)].)
&CMD_SKILLS/SWAP Chargen Object=$+skills/swap *=*:@pemit %#=switch(0,[@@(Does %0 match a skill? Sets grabbed skill into %q0.)]complete(setr(skills,get(%#/_sheet`skills`data`list)),%0,skill,0,|),%qe,[@@(Does %1 match a skill? Sets grabbed skill into %q1.)]complete(%q<skills>,%1,skill,1,|),%qe,[@@(Are the current ratings different? Sets %q0's rating into %qa, and %q1's rating into %qb. Also set the player's _sheet`skills`skills into %q<all>.)]neq(setr(a,u(v(db_sheet)/fun`skill`rating,%#,%q0)),setr(b,u(v(db_sheet)/fun`skill`rating,%#,%q1)))[setq(all,get(%#/_sheet`skills`skills))],Both %q0 and %q1 are rated at [ladder(%qa)]. There is no point in swapping them.,[@@(Is %qa > 0?)]gt(%qa,0),You remove %q1 from your sheet and put %q0 in its place at [ladder(%qb)].[attrib_set(%#/_sheet`skills`skills,[sort(replace(%q<all>,match(%q<all>,%q1`%qb,|),%q0`%qb,|),i,|)])],[@@(Is %qb > 0?)]gt(%qb,0),You remove %q0 from your sheet and put %q0 in its place at [ladder(%qa)].[attrib_set(%#/_sheet`skills`skills,[sort(replace(%q<all>,match(%q<all>,%q0`%qa,|),%q1`%qa,|),i,|)])],You swap the ratings of %q0 (now [ladder(%qb)]) and %q1 (now [ladder(%qa)]).[attrib_set(%#/_sheet`skills`skills,[setdiff(get(%#/_sheet`skills`skills)|%q0`%qb|%q1`%qa,%q0`%qa|%q1`%qb,|,i)])])
&DATA Chargen Object=
&DATA`STUNTS Chargen Object=
&DATA`STUNTS`GO Chargen Object=1,There should be no reason to interrupt this purchase.
&DATA`STUNTS`LISTS Chargen Object=
&DATA`STUNTS`LISTS`BUILD Chargen Object=Initiative Bonus:%q<init>:%q<bonus>~Initiative Bonus~-1~Grants a bonus to initiative. +stunts/buy Build=1=<conflict type>=<num>~[@@(Valid conflict? Sets into %q<init>.)]complete(iter(get(%#/_sheet`init`data`conflicts),first(%i0,`),chr(124),chr(124)),first(%0,=),conflict type,init,chr(124))[setq(init2,edit(%q<init>,%b,_))],%qe,[@@(Valid int? Sets into %q<bonus>.)]isint(setr(bonus,last(%0,=))),Bonus must be a whole number.~You add a %q<bonus> bonus to your %q<init> initiative.[attrib_set(%#/_sheet`init`data`%q<init2>`bonus,[add(get(%#/_sheet`init`data`%q<init2>`bonus),%q<bonus>)])]~You lose your %q<bonus> bonus to %q<init> initiative.\[attrib_set\(\%#/_sheet`init`data`%q<init2>`bonus\,\[sub\(get\(\%#/_sheet`init`data`%q<init2>`bonus\)\,%q<bonus>\)\]\)\]|Initiative Skill Swap:%q<init>:%q<skill>:%q<oldskill>~Initiative Skill Swap~-1~Swaps the skill used for initiative. +stunts/buy Build=2=<conflict type>=<new skill>~[@@(Valid conflict? Sets into %q<init>, sets whole strings into %qa and %qc and old skill into %q<oldskill>.)]complete(iter(setr(a,get(%#/_sheet`init`data`conflicts)),first(%i0,`),chr(124),chr(124)),first(%0,=),conflict type,init,chr(124))[setq(c,grab(%qa,%q<init>*,chr(124)))][setq(oldskill,elements(%qc,2,`))],%qe,[@@(Valid skill? Sets into %q<skill>.)]complete(get(%#/_sheet`skills`data`list),last(%0,=),skill,skill,chr(124)),%qe~You now use %q<skill> for %q<init> initiative.[attrib_set(%#/_sheet`init`data`conflicts,[replace(%qa,match(%qa,%qc,chr(124)),replace(%qc,2,%q<skill>,`),chr(124))])]~You now use %q<oldskill> for %q<init> initiative.\[attrib_set\(\%#/_sheet`init`data`conflicts\,\[replace\(setr\(a\,get\(\%#/_sheet`init`data`conflicts\)\)\,setr\(m\,match\(\%qa\,%q<init>*\,chr\(124\)\)\)\,replace\(elements\(\%qa\,\%qm\,chr\(124\)\)\,2\,%q<oldskill>\,`\)\,chr\(124\)\)\]\)\]|Stress Skill Swap:%q<stress>:%q<skill>:%q<oldskill>~Stress Skill Swap~-1~Swaps the skill used to determine stress capacity. +stunts/buy Build=3=<stress>=<new skill>~[@@(Valid stress track? Sets the whole list into %qa, the whole word into %qs, the old skill into %q<oldskill>, and the particular stress track into %q<stress>.)]complete(iter(setr(a,get(%#/_sheet`stress`data`tracks)),first(%i0,`),chr(124),chr(124)),first(%0,=),track,stress,chr(124))[setq(s,grab(%qa,%q<stress>*,chr(124)))][setq(oldskill,elements(%qs,2,`))],%qe,[@@(Does that track have a determining skill?)]strlen(%q<oldskill>),Your %q<stress> track does not use a skill to determine its capacity.,[@@(Valid skill? Sets into %q<skill>.)]complete(get(%#/_sheet`skills`data`list),last(%0,=),skill,skill,chr(124)),%qe~You now use %q<skill> to determine your %q<stress> capacity.[attrib_set(%#/_sheet`stress`data`tracks,[replace(%qa,match(%qa,%qs,chr(124)),replace(%qs,2,%q<skill>,`),chr(124))])]~You now use %q<oldskill> to determine your %q<stress> capacity.\[attrib_set\(\%#/_sheet`stress`data`tracks\,\[replace\(setr\(a\,get\(\%#/_sheet`stress`data`tracks\)\)\,setr\(m\,match\(\%qa\,%q<stress>*\,chr\(124\)\)\)\,replace\(elements\(\%qa\,\%qm\,chr\(124\)\)\,2\,%q<oldskill>\,`\)\,chr\(124\)\)\]\)\]|Mild Consequences:%q<stress>:%q<bonus>~Bonus Mild Consequences~-1~Grants bonus mild consequences for a certain stress track. +stunts/buy Build=4=<stress>=<num>~[@@(Valid track? Sets it into %q<stress>, and the spaces-stripped-out version into %q<stress2>.)]complete(iter(get(%#/_sheet`stress`data`tracks),first(%i0,`),chr(124),chr(124)),first(%0,=),track,stress,chr(124))[setq(stress2,edit(%q<stress>,%b,_))],%qe,[@@(Valid int? Sets it into %q<bonus>.)]isint(setr(bonus,last(%0,=))),Bonus number must be a whole number.~You gain %q<bonus> bonus mild %q<stress> consequences.[attrib_set(%#/_sheet`stress`%q<stress2>`cons,[add(get(%#/_sheet`stress`%q<stress2>`cons),%q<bonus>)])]~You lose %q<bonus> bonus mild %q<stress> consequences.\[attrib_set\(\%#/_sheet`stress`%q<stress2>`cons\,\[sub\(get\(\%#/_sheet`stress`%q<stress2>`cons\)\,%q<bonus>\)\]\)\]
&DO Chargen Object=
&DO`STRESS Chargen Object=
&DO`STRESS`UPDATE Chargen Object=@@ Action list for re-setting character stress. Takes enactor %0 and target %1.;@@ Nuke existing stress.;@wipe %1/_sheet`stress`tracks`**;@@ Dolist for all existing stress tracks.;@dolist/delimit | [get(%1/_sheet`stress`data`tracks)]={@@ Set stress boxes.;&_sheet`stress`tracks`[setr(0,edit(first(%i0,`),%b,_))] %1=[delete(repeat(%bO,u(fun`stress`boxes,%1,%q0)),0,1)];@@ Set extra consequence slots, if necessary.;@switch u(fun`stress`cons,%1,%q0)=>0,{@dol [lnum(1,%$0)]=&_sheet`stress`tracks`%q0`Mild`%i0 %1=};@@ Set toughness, if necessary.;@switch u(%1/_sheet`stress`%q0`toughness)=>0,&_sheet`stress`tracks`%q0`toughness %1=[delete(repeat(%bO,%$0),0,1)]}
&FUN Chargen Object=
&FUN`STRESS Chargen Object=
&FUN`STRESS`BOXES #71=[@@(Takes player %0 and full stress track name %1. Returns the total number of boxes that stress track would have, accounting for skills, bonuses, and overrides.)]add(udefault(%0/_sheet`stress`%1`override,add(get(%0/_sheet`stress`data`minimum),u(fun`stress`skillboxes,%0,%1))),get(%0/_sheet`stress`%1`bonus))
&FUN`STRESS`CONS Chargen Object=[@@(Takes player %0 and full stress track name %1. Returns the total number of bonus mild consequences, accounting for direct bonus consequences and high skill ratings.)]add([@@(Get bonuses.)]u(%0/_sheet`stress`%1`cons),switch([@@(Get skill name.)]rest(grab(get(%0/_sheet`stress`data`tracks),%1`*,|),`),[@@(Null string)],[@@(Assume zero.)]0,[@@(Else, calculate based on skill rating.)]max(0,div(sub(u(v(db_sheet)/fun`skill`rating,%0,%$0),3),2))))
&FUN`STRESS`SKILLBOXES Chargen Object=[@@(Takes player %0 and full stress track name %1. Returns the number of bonus boxes that skill would grant that stress track.)]switch([@@(Get the skill name for that stress track.)]rest(grab(get(%0/_sheet`stress`data`tracks),%1`*,|),`),,[@@(Null string, so no skill. Zero.)]0,[@@(String, so there's a skill. Switch based on its rating.)]switch(u(v(db_sheet)/fun`skill`rating,%0,%$0),<1,[@@(Below Average. No extra.)]0,<3,[@@(Below Good. +1 box.)]1,[@@(Good and above. +2 boxes.)]2))
&FUN`STUNTS Chargen Object=
&FUN`STUNTS`BUY Chargen Object=[@@(Set the evaluated code name, display name, and once-evaluated on-del code into %qe.)][attrib_set(%!/temp_%#,[elements(%q<stunt>,1 2 7,~)])][setq(e,u(temp_%#))]You buy "[elements(%q<stunt>,2,~)]" for a [switch(%q<cost>,<0,cost,refund)] of [abs(%q<cost>)] stunt slot[switch(abs(%q<cost>),1,,s)]. You now have [add(get(%#/_sheet`refresh),first(%q<calc>))] refresh and [switch(last(%q<calc>),0,no free stunts,1,1 free stunt,%$0 free stunts)] remaining.[@@(Set on-buy attribute.)][attrib_set(%!/temp_%#,[switch(elements(%q<stunt>,6,~),[@@(null string)],No special changes.,:*,[@@(Get attribute)][v(data`stunts`[delete(%$0,0,1)])],[@@(Run code.)]%$0)])]%r[u(ansi)] [u(temp_%#)][@@(Set the on-del into %qd.)][attrib_set(%!/temp_%#,elements(%q<stunt>,7,~))][setq(d,u(temp_%#))][@@(Set the player's stunt list.)][attrib_set(%#/_sheet`stunts`stunts,[insert(get(%#/_sheet`stunts`stunts),-1,[@@(Codename and display name.)][elements(%qe,1 2,~)]~[@@(Cost.)]%q<cost>~~[@@(On-del.)][last(%qe,~)],|)])][@@(If the on-del is an attribute, set it, once-evaluated, into the player.)][switch(%qd,:*,attrib_set(%#/_sheet`stunts`del`[delete(%$0,0,1)],[u(data`stunts`[delete(%$0,0,1)])]))][@@(Wipe the temp attribute.)][attrib_set(%!/temp_%#)]
&FUN`STUNTS`TEST1 Chargen Object=u(ansi) [switch(0,
&FUN`STUNTS`TEST2 Chargen Object=,u(fun`stunts`buy))]
